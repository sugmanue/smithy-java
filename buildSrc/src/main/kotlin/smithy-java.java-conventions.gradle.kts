import com.github.spotbugs.snom.Effort
import org.gradle.accessors.dm.LibrariesForLibs
import software.amazon.smithy.java.buildtools.ShortenFullyQualifiedNames

plugins {
    `java-library`
    id("com.adarshr.test-logger")
    id("com.github.spotbugs")
    id("com.diffplug.spotless")
    id("com.autonomousapps.dependency-analysis")
    id("info.solidsoft.pitest")
    id("smithy-java.utilities")
}

// Workaround per: https://github.com/gradle/gradle/issues/15383
val Project.libs get() = the<LibrariesForLibs>()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

tasks.withType<JavaCompile>() {
    options.encoding = "UTF-8"
    options.release.set(21)
}

tasks.withType<Javadoc>() {
    options.encoding = "UTF-8"
}

tasks.withType<AbstractArchiveTask> {
    isPreserveFileTimestamps = false
    isReproducibleFileOrder = true
}

/*
 * Common test configuration
 * ===============================
 */
dependencies {
    testImplementation(platform(libs.junit.bom))
    testImplementation(libs.junit.jupiter.api)
    testRuntimeOnly(libs.junit.jupiter.engine)
    testImplementation(libs.junit.jupiter.params)
    testImplementation(libs.junit.platform.launcher)
    testImplementation(libs.hamcrest)
    testImplementation(libs.assertj.core)
    compileOnly("com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}")
    testCompileOnly("com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}")
    "pitest"(libs.pitest.junit5.plugin)
}

tasks.withType<Test> {
    useJUnitPlatform()
}

testlogger {
    showExceptions = true
    showStackTraces = true
    showFullStackTraces = false
    showCauses = true
    showSummary = false
    showPassed = false
    showSkipped = false
    showFailed = true
    showOnlySlow = false
    showStandardStreams = true
    showPassedStandardStreams = false
    showSkippedStandardStreams = false
    showFailedStandardStreams = true
    logLevel = LogLevel.WARN
}

/*
 * Formatting
 * ==================
 * see: https://github.com/diffplug/spotless/blob/main/plugin-gradle/README.md#java
 */
spotless {
    java {
        // Enforce a common license header on all files
        licenseHeaderFile("${project.rootDir}/config/spotless/license-header.txt")
            .onlyIfContentMatches("^((?!SKIPLICENSECHECK)[\\s\\S])*\$")
        leadingTabsToSpaces()
        endWithNewline()

        eclipse("4.40").configFile("${project.rootDir}/config/spotless/formatting.xml")

        addStep(ShortenFullyQualifiedNames.createStep())

        // Static first, then everything else alphabetically
        removeUnusedImports()
        importOrder("\\#", "")
        // Ignore generated generated code for formatter check
        targetExclude("**/build/**/*.java", "**/build/generated-src*/*.*")
    }

    // Formatting for build.gradle.kts files
    kotlinGradle {
        // TODO: re-enable once ktlint 2.0.0 is released with JDK 25 support
        // (ktlint 1.x embeds Kotlin 2.2 which cannot initialize on JDK 25)
        // ktlint()
        leadingTabsToSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

/*
 * Spotbugs
 * ====================================================
 *
 * Run spotbugs against source files and configure suppressions.
 */
// Configure the spotbugs extension.
spotbugs {
    effort = Effort.MAX
    excludeFilter = file("${project.rootDir}/config/spotbugs/filter.xml")
}

// We don't need to lint tests.
tasks.named("spotbugsTest") {
    enabled = false
}

pitest {
    targetClasses.set(setOf("software.amazon.smithy.*"))
    targetTests.set(setOf("software.amazon.smithy.*"))
    excludedClasses.set(setOf("*.GeneratedVersionProvider"))
    threads.set(Runtime.getRuntime().availableProcessors())
    outputFormats.set(setOf("HTML", "XML"))
    timestampedReports.set(false)
    mutationThreshold.set(0)
    failWhenNoMutations.set(false)
}

tasks.named("pitest") {
    val reportDir = project.layout.buildDirectory.dir("reports/pitest").map { it.asFile.absolutePath }
    doLast {
        val dir = reportDir.get()
        logger.lifecycle("Pitest HTML report: file://${dir}/index.html")
        logger.lifecycle("Pitest XML report: file://${dir}/mutations.xml")
    }
}

/*
 * Repositories
 * ================================
 */
repositories {
    mavenLocal()
    mavenCentral()
}
